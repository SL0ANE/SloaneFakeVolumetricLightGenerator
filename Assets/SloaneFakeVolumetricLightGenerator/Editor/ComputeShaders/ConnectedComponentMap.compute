#pragma kernel Initialize
#pragma kernel FloodFill
#pragma kernel CountConnectedComponent
#pragma kernel InitializeFragment
#pragma kernel InitializeSubFragment
#pragma kernel FillSegmentSet
#pragma kernel GenerateVertexData

RWTexture2D<float4> _SourceTexture;
RWTexture2D<float4> _ConnectedComponentMap;
RWTexture2D<float4> _PrevConnectedComponentMap;

uint _Width;
uint _Height;

int _BoundingOffsetx;
int _BoundingOffsety;
uint _BoundingWidth;
uint _BoundingHeight;

RWStructuredBuffer<int> _FloodFlag;

RWStructuredBuffer<float4> _ConnectedComponentSet;
RWStructuredBuffer<int> _ConnectedComponentIndex;

RWTexture2D<float4> _ConnectedComponentFragment;
float4 _FragmentIndex;

RWStructuredBuffer<int4> _ConnectedSegmentSet;
RWStructuredBuffer<int> _ConnectedSegmentIndex;

uint _IndexOffset;
uint _TotalSegments;
float4 _ProjectionFactor;
float _CastDistance;

RWStructuredBuffer<float3> _VerticesBuffer;
RWStructuredBuffer<int> _IndicesBuffer;

[numthreads(8,8,1)]
void Initialize(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= _Width || id.y >= _Height)
    {
        return;
    }

    float4 sourcePixel = _SourceTexture[id.xy];
    float4 connectedComponent = float4(0.0, 0.0, 0.0, 1.0);

    if (sourcePixel.r <= 0.0)
    {
        connectedComponent = float4(float(id.x) / _Width, float(id.y) / _Height, 1.0, 1.0);
    }

    _ConnectedComponentMap[id.xy] = connectedComponent;
}

void CompareAndFlood(inout float4 center, float4 reference)
{
    if (reference.b > 0.0)
    {
        if (center.x > reference.x)
        {
            center.x = reference.x;
            center.y = reference.y;
            InterlockedAdd(_FloodFlag[0], 1);
        }
        else if (center.x == reference.x && center.y > reference.y)
        {
            center.x = reference.x;
            center.y = reference.y;
            InterlockedAdd(_FloodFlag[0], 1);
        }
    }
}

[numthreads(8,8,1)]
void FloodFill(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= _Width || id.y >= _Height)
    {
        return;
    }

    float4 centerPixel = _PrevConnectedComponentMap[id.xy];

    if(centerPixel.b == 0.0)
    {
        return;
    }

    float4 leftPixel = (id.x > 0) ? _PrevConnectedComponentMap[id.xy - uint2(1.0, 0.0)] : float4(0.0, 0.0, 0.0, 1.0);
    float4 rightPixel = (id.x < _Width - 1) ? _PrevConnectedComponentMap[id.xy + uint2(1.0, 0.0)] : float4(0.0, 0.0, 0.0, 1.0);
    float4 topPixel = (id.y > 0) ? _PrevConnectedComponentMap[id.xy - uint2(0.0, 1.0)] : float4(0.0, 0.0, 0.0, 1.0);
    float4 bottomPixel = (id.y < _Height - 1) ? _PrevConnectedComponentMap[id.xy + uint2(0.0, 1.0)] : float4(0.0, 0.0, 0.0, 1.0);

    CompareAndFlood(centerPixel, leftPixel);
    CompareAndFlood(centerPixel, rightPixel);
    CompareAndFlood(centerPixel, topPixel);
    CompareAndFlood(centerPixel, bottomPixel);

    _ConnectedComponentMap[id.xy] = centerPixel;
}

[numthreads(8,8,1)]
void CountConnectedComponent(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= _Width || id.y >= _Height)
    {
        return;
    }

    float4 connectedComponent = _ConnectedComponentMap[id.xy];

    if (connectedComponent.b == 0.0)
    {
        return;
    }

    int lockValue = 0;
    InterlockedCompareExchange(_ConnectedComponentIndex[1], 1, 0, lockValue);

    for (int i = 0; i < _ConnectedComponentIndex[0]; i++)
    {
        float4 reference = _ConnectedComponentSet[i];

        if (reference.x == connectedComponent.x && reference.y == connectedComponent.y)
        {
            return;
        }
    }

    int index = 0;
    InterlockedAdd(_ConnectedComponentIndex[0], 1, index);
    _ConnectedComponentSet[index] = connectedComponent;

    InterlockedExchange(_ConnectedComponentIndex[1], 0, lockValue);
}

[numthreads(8,8,1)]
void InitializeFragment(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= _BoundingWidth || id.y >= _BoundingHeight)
    {
        return;
    }

    int2 signedCoord = int2(id.x + _BoundingOffsetx, id.y + _BoundingOffsety);
    if (signedCoord.x < 0 || signedCoord.x >= int(_Width) || signedCoord.y < 0 || signedCoord.y >= int(_Height))
    {
        _ConnectedComponentFragment[id.xy] = float4(float(id.x) / _BoundingWidth, float(id.y) / _BoundingHeight, 1.0, 1.0);
        return;
    }

    uint2 coord = uint2(signedCoord);
    float4 color = _ConnectedComponentMap[coord];
    float4 outputColor;

    if(color.r == _FragmentIndex.r && color.g == _FragmentIndex.g)
    {
        outputColor = float4(0.0, 0.0, 0.0, 1.0);
    }
    else
    {
        outputColor = float4(float(id.x) / _BoundingWidth, float(id.y) / _BoundingHeight, 1.0, 1.0);
    }

    _ConnectedComponentFragment[id.xy] = outputColor;
}

[numthreads(8,8,1)]
void InitializeSubFragment(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= _BoundingWidth || id.y >= _BoundingHeight)
    {
        return;
    }

    int2 signedCoord = int2(id.x + _BoundingOffsetx, id.y + _BoundingOffsety);
    if (signedCoord.x < 0 || signedCoord.x >= int(_Width) || signedCoord.y < 0 || signedCoord.y >= int(_Height))
    {
        _ConnectedComponentFragment[id.xy] = float4(1.0, 1.0, 1.0, 1.0);
        return;
    }

    uint2 coord = uint2(signedCoord);
    float4 color = _ConnectedComponentMap[coord];
    float4 outputColor;

    if(color.r == _FragmentIndex.r && color.g == _FragmentIndex.g && color.b == 1.0)
    {
        outputColor = float4(0.0, 0.0, 0.0, 1.0);
    }
    else
    {
        outputColor = float4(1.0, 1.0, 1.0, 1.0);
    }

    _ConnectedComponentFragment[id.xy] = outputColor;
}

void AddSegment(int2 startCoord, int2 endCoord)
{
    int index = 0;
    InterlockedAdd(_ConnectedSegmentIndex[0], 1, index);

    int4 segment = int4(startCoord.x, startCoord.y, endCoord.x, endCoord.y);
    _ConnectedSegmentSet[index] = segment;
}

void HandleSegment(int2 centerIndex, int caseIndex)
{
    if(caseIndex == 0)
    {
        // ⬚◻⬚
        // ◻◻◻
        // ⬚◻⬚
        return;
    }
    if(caseIndex == 1)
    {
        // ⬚◻⬚   
        // ◻◻◼
        // ⬚◻⬚
        AddSegment(centerIndex + int2(1, 1), centerIndex + int2(1, 0));
        return;
    }
    if(caseIndex == 2)
    {
        // ⬚◻⬚
        // ◻◻◻
        // ⬚◼⬚
        AddSegment(centerIndex + int2(1, 0), centerIndex + int2(0, 0));
        return;
    }
    if(caseIndex == 3)
    {
        // ⬚◻⬚
        // ◻◻◼
        // ⬚◼⬚
        AddSegment(centerIndex + int2(1, 1), centerIndex + int2(0, 0));
        return;
    }
    if(caseIndex == 4)
    {
        // ⬚◻⬚
        // ◼◻◻
        // ⬚◻⬚
        AddSegment(centerIndex + int2(0, 0), centerIndex + int2(0, 1));
        return;
    }
    if(caseIndex == 5)
    {
        // ⬚◻⬚
        // ◼◻◼
        // ⬚◻⬚
        AddSegment(centerIndex + int2(0, 0), centerIndex + int2(0, 1));
        AddSegment(centerIndex + int2(1, 1), centerIndex + int2(1, 0));
        return;
    }
    if(caseIndex == 6)
    {
        // ⬚◻⬚
        // ◼◻◻
        // ⬚◼⬚
        AddSegment(centerIndex + int2(1, 0), centerIndex + int2(0, 1));
        return;
    }
    if(caseIndex == 7)
    {
        // ⬚◻⬚
        // ◼◻◼
        // ⬚◼⬚
        AddSegment(centerIndex + int2(1, 1), centerIndex + int2(0, 1));
        return;
    }
    if(caseIndex == 8)
    {
        // ⬚◼⬚
        // ◻◻◻
        // ⬚◻⬚
        AddSegment(centerIndex + int2(0, 1), centerIndex + int2(1, 1));
        return;
    }
    if(caseIndex == 9)
    {
        // ⬚◼⬚
        // ◻◻◼
        // ⬚◻⬚
        AddSegment(centerIndex + int2(0, 1), centerIndex + int2(1, 0));
        return;
    }
    if(caseIndex == 10)
    {
        // ⬚◼⬚
        // ◻◻◻
        // ⬚◼⬚
        AddSegment(centerIndex + int2(0, 1), centerIndex + int2(1, 1));
        AddSegment(centerIndex + int2(1, 0), centerIndex + int2(0, 0));
        return;
    }
    if(caseIndex == 11)
    {
        // ⬚◼⬚
        // ◻◻◼
        // ⬚◼⬚
        AddSegment(centerIndex + int2(0, 1), centerIndex + int2(0, 0));
        return;
    }
    if(caseIndex == 12)
    {
        // ⬚◼⬚
        // ◼◻◻
        // ⬚◻⬚
        AddSegment(centerIndex + int2(0, 0), centerIndex + int2(1, 1));
        return;
    }
    if(caseIndex == 13)
    {
        // ⬚◼⬚
        // ◼◻◼
        // ⬚◻⬚
        AddSegment(centerIndex + int2(0, 0), centerIndex + int2(1, 0));
        return;
    }
    if(caseIndex == 14)
    {
        // ⬚◼⬚
        // ◼◻◻
        // ⬚◼⬚
        AddSegment(centerIndex + int2(1, 0), centerIndex + int2(1, 1));
        return;
    }
    if(caseIndex == 15)
    {
        // ⬚◼⬚
        // ◼◻◼
        // ⬚◼⬚
        return;
    }
}

[numthreads(8,8,1)]
void FillSegmentSet(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= _Width || id.y >= _Height)
    {
        return;
    }

    int centerValue = _ConnectedComponentFragment[id.xy].b > 0.0 ? 1 : 0;
    if(centerValue == 1)
    {
        // 有内容就不处理线框
        return;
    }

    int rightValue = id.x < _Width - 1 ? (_ConnectedComponentFragment[id.xy + uint2(1.0, 0.0)].b > 0.0 ? 1 : 0) : 0;
    int bottomValue = id.y > 0 ? (_ConnectedComponentFragment[id.xy - uint2(0.0, 1.0)].b > 0.0 ? 1 : 0) : 0;
    int leftValue = id.x > 0 ? (_ConnectedComponentFragment[id.xy - uint2(1.0, 0.0)].b > 0.0 ? 1 : 0) : 0;
    int topValue = id.y < _Height - 1 ? (_ConnectedComponentFragment[id.xy + uint2(0.0, 1.0)].b > 0.0 ? 1 : 0) : 0;

    int caseIndex = rightValue + (bottomValue << 1) + (leftValue << 2) + (topValue << 3);
    HandleSegment(int2(id.xy), caseIndex);
}

[numthreads(8,8,1)]
void GenerateVertexData(uint3 id : SV_DispatchThreadID)
{
    uint sourceIndex = id.x * 8 + id.y;
    if (sourceIndex >= _TotalSegments)
    {
        return;
    }

    int verticeIndexStart = 2 * (sourceIndex + _IndexOffset);
    int4 segment = _ConnectedSegmentSet[sourceIndex];

    int2 startCoord = int2(segment.x, segment.y) + int2(_BoundingOffsetx, _BoundingOffsety);
    float3 nearPlanePoint = float3(float(startCoord.x) * _ProjectionFactor.x, float(startCoord.y) * _ProjectionFactor.y, 0.0);
    float3 farPlanePoint = float3(float(startCoord.x) * _ProjectionFactor.z, float(startCoord.y) * _ProjectionFactor.w, _CastDistance);

    _VerticesBuffer[verticeIndexStart] = nearPlanePoint;
    _VerticesBuffer[verticeIndexStart + 1] = farPlanePoint;

    int currIndexStart = sourceIndex * 2;
    int nextIndexStart = sourceIndex * 2 + 2;
    if (sourceIndex == _TotalSegments - 1) nextIndexStart = 0;
    currIndexStart += _IndexOffset * 2;
    nextIndexStart += _IndexOffset * 2;

    int indiceIndexStart = 6 * (sourceIndex + _IndexOffset);
    _IndicesBuffer[indiceIndexStart] = nextIndexStart;
    _IndicesBuffer[indiceIndexStart + 1] = currIndexStart + 1;
    _IndicesBuffer[indiceIndexStart + 2] = currIndexStart;
    _IndicesBuffer[indiceIndexStart + 3] = nextIndexStart + 1;
    _IndicesBuffer[indiceIndexStart + 4] = currIndexStart + 1;
    _IndicesBuffer[indiceIndexStart + 5] = nextIndexStart;
}